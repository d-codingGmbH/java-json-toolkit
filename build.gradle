plugins {
  id "java"
}

import java.text.SimpleDateFormat

def firstCopyrightYear = 2022
def buildDate          = new Date()
def currentYear        = Calendar.getInstance().get(Calendar.YEAR)
def vendorName         = "d-coding GmbH"
def vendorUrl          = "https://www.d-coding.de"

def tz = TimeZone.getTimeZone("UTC")
def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss'Z'")
df.setTimeZone(tz)

def buildDateString = df.format(buildDate)

def sourceCompatibility = 1.7
def targetCompatibility = 1.7
  
compileJava.options.encoding     = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

allprojects {
  repositories {
    mavenCentral()
  }
}

subprojects {
  apply plugin: "java"
  apply plugin: "maven-publish"
  apply plugin: "jacoco"

  group   = "de.d-coding"
  version = "1.0.3"

  java {
    withSourcesJar()
    withJavadocJar()
  }

  ext {
    projectDescription = ""
  }

  jar {
    onlyIf {
      !sourceSets.main.allSource.files.isEmpty()
    }
    manifest {
      attributes(
        "Implementation-Title":       project.name,
        "Implementation-Version":     project.version,
        "Implementation-Vendor":      vendorName,
        "Vendor-URL":                 vendorUrl,
        "Copyright":                  "Â© ${(firstCopyrightYear < currentYear) ? (firstCopyrightYear + "-") : ""}${currentYear} by ${vendorName}",
        "Built-By":                   vendorName,
        "Build-Date":                 buildDateString,
        "Build-Jdk":                  "${System.properties["java.version"]} (${System.properties["java.vendor"]} ${System.properties["java.vm.version"]})",
        "Build-OS":                   "${System.properties["os.name"]} ${System.properties["os.arch"]} ${System.properties["os.version"]}",
        "Created-By":                 "Gradle ${gradle.gradleVersion}",
        "Main-Class":                 "de.dcoding.Application"
      )
    }
  }

  afterEvaluate {
    publishing {
      publications {
        "$project.name"(MavenPublication) {
          pom {
            artifactId = project.name
            from components.java
            name = "${group}:${project.name}"
            description = projectDescription
            url = "https://github.com/d-codingGmbH/java-json-toolkit"
            organization {
              name = vendorName
              url  = vendorUrl
            }
            developers {
              developer {
                name = "David Ullrich"
                email = "david.ullrich@d-coding.de"
                organization = vendorName
                organizationUrl = vendorUrl
              }
            }
            licenses {
              license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
              }
            }
            scm {
              connection = "scm:git:https://github.com/d-codingGmbH/java-json-toolkit.git"
              url = "https://github.com/d-codingGmbH/java-json-toolkit"
            }
          }
        }
      }
    }
  }

  test {
    finalizedBy jacocoTestReport
  }

  jacocoTestReport {
    dependsOn test
  }
}
